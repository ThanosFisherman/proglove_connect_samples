apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("./keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
if (keystorePropertiesFile.exists() && keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "de.proglove.example.sdk"
        minSdkVersion 18
        targetSdkVersion 33
        versionCode current_version_app.toInteger()
        versionName "1.1"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (keystoreProperties['KEY_ALIAS'] != null) {
                keyAlias keystoreProperties['KEY_ALIAS'].toString()
            }
            if (keystoreProperties['KEY_PASSWORD'] != null) {
                keyPassword keystoreProperties['KEY_PASSWORD'].toString()
            }
            if (keystoreProperties['STORE_FILE'] != null) {
                storeFile rootProject.file(keystoreProperties['STORE_FILE'])
            }
            if (keystoreProperties['STORE_PASSWORD'] != null) {
                storePassword keystoreProperties['STORE_PASSWORD'].toString()
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "com.google.android.material:material:1.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.recyclerview:recyclerview:1.3.1"
    if (findProject(':connect-sdk') != null) {
        implementation project(':connect-sdk')
    } else {
        implementation "de.proglove:connect-sdk:$current_version_scanner_api"
    }
    implementation project(':common')
}
